#include "../Complex.hlsl"
#include "../Math.hlsl"
#include "../Random.hlsl"
#include "../Material.hlsl"

#pragma kernel InitSpectrum
#pragma kernel FftColumns
#pragma kernel CalculateNormals
#pragma kernel GenerateFilteredMips

cbuffer InitSpectrumProperties
{
	float4 _OceanScale, SpectrumStart, SpectrumEnd;
	float _OceanGravity, Time;
};

RWTexture2DArray<float4> targetTexture;
RWTexture2DArray<float2> targetTexture1;

static const uint _Resolution = 128;
static const uint Log2Resolution = log2(_Resolution);
static const uint HalfResolution = _Resolution >> 1;

struct OceanSpectrum
{
	float scale;
	float windSpeed;
	float windAngle;
	float fetch;
	float spreadBlend;
	float swell;
	float peakEnhancement;
	float shortWavesFade;
};

cbuffer OceanData
{
	float scale0;
	float windSpeed0;
	float windAngle0;
	float fetch0;
	float spreadBlend0;
	float swell0;
	float peakEnhancement0;
	float shortWavesFade0;
	float scale1;
	float windSpeed1;
	float windAngle1;
	float fetch1;
	float spreadBlend1;
	float swell1;
	float peakEnhancement1;
	float shortWavesFade1;
};

OceanSpectrum LocalSpectrum()
{
	OceanSpectrum s;
	s.scale = scale0;
	s.windSpeed = windSpeed0;
	s.windAngle = windAngle0;
	s.fetch = fetch0;
	s.spreadBlend = spreadBlend0;
	s.swell = swell0;
	s.peakEnhancement = peakEnhancement0;
	s.shortWavesFade = shortWavesFade0;
	return s;
}

groupshared float2 _HShared[2][_Resolution];
groupshared float2 _DxShared[2][_Resolution];
groupshared float2 _DzShared[2][_Resolution];

void Fft(uint id, inout float2 height, inout float2 dispX, inout float2 dispZ)
{
    // Load entire row or column into scratch array
	_HShared[0][id] = height;
	_DxShared[0][id] = dispX;
	_DzShared[0][id] = dispZ;

	uint2 textureIndices = uint2(0, 1);
	
	uint2 indices = 0;
	uint passCount = log2(_Resolution);
	for (uint i = 0; i < passCount; i++)
	{
		GroupMemoryBarrierWithGroupSync();

		uint sectionWidth = 2 << i;
		uint halfSectionWidth = sectionWidth / 2;

		uint sectionStartOffset = id & ~(sectionWidth - 1);
		uint halfSectionOffset = id & (halfSectionWidth - 1);
		uint sectionOffset = id & (sectionWidth - 1);

		uint2 indices = sectionStartOffset + halfSectionOffset;
		indices.y += halfSectionWidth;

		if (i == 0)
			indices = reversebits(indices) >> (32 - passCount) & (_Resolution - 1);
		
		float2 weights = cexp(cimg(2.0 * Pi * sectionOffset / sectionWidth));

		height = _HShared[textureIndices.y][id] = cadd(_HShared[textureIndices.x][indices.x], cmul(_HShared[textureIndices.x][indices.y], weights));
		dispX = _DxShared[textureIndices.y][id] = cadd(_DxShared[textureIndices.x][indices.x], cmul(_DxShared[textureIndices.x][indices.y], weights));
		dispZ = _DzShared[textureIndices.y][id] = cadd(_DzShared[textureIndices.x][indices.x], cmul(_DzShared[textureIndices.x][indices.y], weights));

		textureIndices.xy = textureIndices.yx;
	}
}

float PhillipsSpectrum(float2 k, float A, float kl)
{
	// Phillips spectrum
	float2 windDirection = float2(cos(windAngle0 * TwoPi), sin(windAngle0 * TwoPi));
	float windFactor = dot(normalize(k), windDirection);
	float L = Sq(windSpeed0) / _OceanGravity; // Largest possible waves arising from a continuous wind speed of V

	// Eq 40
	float p = A * exp(-1.0 / Sq(kl * L)) / pow(kl, 4.0) * pow(windFactor, 6.0);
	
	// Eq 41
	p *= exp(-dot(k, k) * Sq(shortWavesFade0)); // Eq 41: Supress small wavelengths
	
	// Move waves along wind direction
	p *= dot(normalize(k), windDirection) < 0 ? (1 - swell0) : 1;

	return p;
}

[numthreads(_Resolution, 1, 1)]
void InitSpectrum(uint3 id : SV_DispatchThreadID)
{
	float rcpPatchSize = _OceanScale[id.z];

	int n = id.x - HalfResolution;
	int m = id.y - HalfResolution;
	
	float2 k = TwoPi * rcpPatchSize * float2(n, m);
	
	float rcpFrequency = (n && m) ? rsqrt(dot(k, k)) : 1.0;
	float frequency = rcpFrequency ? rcp(rcpFrequency) : 1.0;
	
	// Eq 31
	float w = sqrt(_OceanGravity * frequency);
	
	// Eq 35 (Todo: precalculate)
	float w0 = TwoPi / 200.0;
	float dispersion = floor(w / w0) * w0;
	
	// Eq 43
	float wtk = dispersion * Time * 1;
	float4 xi = GaussianFloat4(id.x + id.y * _Resolution);
	
	// Eq 42 (Todo: precalculate)
	float fftNorm = pow(_Resolution, -0.25);
	float philNorm = exp(1.0) * rcpPatchSize;
	float A = rsqrt(2.0) * Sq(fftNorm * philNorm);
	float4 h0 = 1.0 / sqrt(2.0) * xi * sqrt(float2(PhillipsSpectrum(k, A, frequency), PhillipsSpectrum(-k, A, frequency))).xxyy;

	float2 h = czero(), dispX = czero(), dispZ = czero(), slopeX = czero(), slopeZ = czero();

	float nr = length(float2(n, m));
	if (n && m && nr >= SpectrumStart[id.z] && nr < SpectrumEnd[id.z])
	{
		// Eq 36
		h = cmul(h0.xy, cexp(cimg(wtk)));
		h = cadd(h, cmul(h0.zw, cexp(-cimg(wtk))));
		
		// Eq 37
		slopeX = cmul(cimg(k.x), h);
		slopeZ = cmul(cimg(k.y), h);
		
		// Eq 44
		dispX = cmul(-cimg(k.x * rcpFrequency), h);
		dispZ = cmul(-cimg(k.y * rcpFrequency), h);
	}
	
	// Perform the FFT
	Fft(id.x, h, dispX, dispZ);
	
	// Output results to textures
	targetTexture1[id] = h;
	targetTexture[id] = float4(dispX, dispZ);
}

RWTexture2DArray<float3> DisplacementOutput;
Texture2DArray<float3> DisplacementInput;
RWTexture2DArray<float4> sourceTexture;
RWTexture2DArray<float2> sourceTexture1;

RWTexture2DArray<float4> SmoothnessOutput0, SmoothnessOutput1, SmoothnessOutput2, SmoothnessOutput3, SmoothnessOutput4, SmoothnessOutput5, SmoothnessOutput6, SmoothnessOutput7;
RWTexture2DArray<float4> OceanNormalFoamSmoothness;

float _OceanScale0, _OceanWindSpeed0, _OceanWindDirection0, _OceanFetch0, _OceanSpreadBlend0, _OceanSwell0, _OceanPeakEnhancement0, _OceanShortWavesFade0;
float _OceanScale1, _OceanWindSpeed1, _OceanWindDirection1, _OceanFetch1, _OceanSpreadBlend1, _OceanSwell1, _OceanPeakEnhancement1, _OceanShortWavesFade1;
uint Size;

[numthreads(_Resolution, 1, 1)]
void FftColumns(uint3 id : SV_DispatchThreadID)
{
	uint2 texturePos = uint2(id.yx);

    // Load entire row or column into scratch array
	float2 height = sourceTexture1[uint3(texturePos, id.z)];
	float4 displacement = sourceTexture[uint3(texturePos, id.z)];

	float2 h = height;
	float2 dispX = displacement.xy;
	float2 dispZ = displacement.zw;
	
	Fft(id.x, h, dispX, dispZ);
	
	float sign = ((id.x + id.y) & 1) == 0 ? 1.0 : -1.0;
	DisplacementOutput[uint3(texturePos, id.z)] = float3(-dispX.r, h.r, -dispZ.r) * sign;
}

cbuffer CalculateNormalsProperties
{
	float4 _CascadeTexelSizes;
	float Smoothness;
	float _FoamStrength, _FoamDecay, _FoamThreshold;
	uint _OceanTextureSlicePreviousOffset;
};

[numthreads(8, 8, 1)]
void CalculateNormals(uint3 id : SV_DispatchThreadID)
{
    // Original change in x/y
	float delta = _CascadeTexelSizes[id.z];

	float3 left = DisplacementInput[int3((id.xy + int2(-1, 0)) % _Resolution, id.z)];
	float3 right = DisplacementInput[int3((id.xy + int2(1, 0)) % _Resolution, id.z)];
	float3 down = DisplacementInput[int3((id.xy + int2(0, -1)) % _Resolution, id.z)];
	float3 up = DisplacementInput[int3((id.xy + int2(0, 1)) % _Resolution, id.z)];

	float3 dx = (right - left) / (2.0 * delta);
	float3 dy = (up - down) / (2.0 * delta);
	
	float2 normalGradients = float2(-dx.y, -dy.y);
	
	float3 normal = normalize(float3(normalGradients, 1.0));
	normal *= SmoothnessToNormalLength(Smoothness);
	
	// Eq 45
	float jxx = 1.0 + dx.x;
	float jyy = 1.0 + dy.z;
	float jxy = dx.x;
	float jyx = dy.z;
	
	float jacobian = jxx * jyy - jxy * jyx;
	OceanNormalFoamSmoothness[uint3(id.xy, id.z)] = float4(normal.xy, jacobian, 2.0 * Smoothness - 1.0); // Snorm texture, need to put smoothness into -1 to 1 range
}

// Thread group size of 32 is required to process  the entire 128x128 texture in shared memory
groupshared float4 mipDataShared[32][32];

static const uint ThreadCount = 32;

float4 UnpackData(float4 data)
{
	return float4(UnpackNormalSNorm(data.rg) * SmoothnessToNormalLength(0.5 * data.a + 0.5), data.b);
}

[numthreads(32, 32, 1)]
void GenerateFilteredMips(uint2 groupThreadID : SV_GroupThreadID, uint2 groupID : SV_GroupID, uint2 dispatchThreadID : SV_DispatchThreadID)
{
	uint depth = groupID.x;
	groupID = 0;
	dispatchThreadID.x %= Size;

	RWTexture2DArray<float4> result[8] = { SmoothnessOutput0, SmoothnessOutput1, SmoothnessOutput2, SmoothnessOutput3, SmoothnessOutput4, SmoothnessOutput5, SmoothnessOutput6, SmoothnessOutput7 };
	float4 mipDataSum = 0.0;

    [unroll]
	for (uint y = 0; y < 2; y++)
	{
        [unroll]
		for (uint x = 0; x < 2; x++)
		{
			uint2 offset = dispatchThreadID * 4 + 2 * uint2(x, y);

			// TODO: Gather?
			float4 mipData = UnpackData(SmoothnessOutput0[uint3(offset + uint2(0, 0), depth)]);
			mipData += UnpackData(SmoothnessOutput0[uint3(offset + uint2(1, 0), depth)]);
			mipData += UnpackData(SmoothnessOutput0[uint3(offset + uint2(0, 1), depth)]);
			mipData += UnpackData(SmoothnessOutput0[uint3(offset + uint2(1, 1), depth)]);
			mipData *= 0.25;

			float3 averageNormal = normalize(mipData.xyz);
			uint3 dst = uint3(dispatchThreadID * 2 + uint2(x, y), depth);
			result[1][dst] = float4(averageNormal.xy, mipData.w, 2.0 * LengthToSmoothness(length(mipData.xyz)) - 1.0);
			mipDataSum += mipData;
		}
	}

	mipDataSum *= 0.25;
	float3 averageNormal = normalize(mipDataSum.xyz);
	uint3 dst = uint3(dispatchThreadID, depth);
	result[2][dst] = float4(averageNormal.xy, mipDataSum.w, 2.0 * LengthToSmoothness(length(mipDataSum.xyz)) - 1.0);

	mipDataShared[groupThreadID.x][groupThreadID.y] = mipDataSum;

    [unroll]
	for (uint s = ThreadCount >> 1, i = 3; s > 0; s >>= 1, i++)
	{
		GroupMemoryBarrierWithGroupSync();
		
		float4 mipDataSum = 0.0;
		if (all(groupThreadID < s))
		{
            // Load four normals and average
			mipDataSum = mipDataShared[groupThreadID.x][groupThreadID.y];
			mipDataSum += mipDataShared[groupThreadID.x + s][groupThreadID.y];
			mipDataSum += mipDataShared[groupThreadID.x][groupThreadID.y + s];
			mipDataSum += mipDataShared[groupThreadID.x + s][groupThreadID.y + s];
			mipDataSum *= 0.25;

			float3 averageNormal = normalize(mipDataSum.xyz);
			uint3 dst = uint3(groupThreadID, depth);
			result[i][dst] = float4(averageNormal.xy, mipDataSum.w, 2.0 * LengthToSmoothness(length(mipDataSum.xyz)) - 1.0);
		}
		
		GroupMemoryBarrierWithGroupSync();
		
		if (all(groupThreadID < s))
		{
			uint2 ind = (groupThreadID / 2) + (groupThreadID % 2) * (s / 2);
			mipDataShared[ind.x][ind.y] = mipDataSum;
		}
	}
}
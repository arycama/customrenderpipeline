#include "../../Common.hlsl"
#include "../../IndirectRendering.hlsl"
#include "../../Packing.hlsl"
#include "../../SpaceTransforms.hlsl"

#pragma kernel InstanceCompaction

// Since we use groupId to index the instance prefix sum, this needs to match
const static uint threadCount = 1024;
const static uint log2ThreadCount = firstbitlow(threadCount);

uint MaxThread;

RWStructuredBuffer<uint> Output;
RWStructuredBuffer<uint> SortKeysWrite;
RWStructuredBuffer<uint> LodCounts;

StructuredBuffer<uint> Input;
StructuredBuffer<uint> GroupSums, PrefixSums;
StructuredBuffer<float4> InstanceBounds;

StructuredBuffer<uint> InstanceTypeIds;
StructuredBuffer<InstanceTypeData> InstanceTypeDatas;
StructuredBuffer<InstanceTypeLodData> InstanceTypeLodDatas;
StructuredBuffer<float> LodSizes;

[numthreads(threadCount, 1, 1)]
void InstanceCompaction(uint id : SV_DispatchThreadID, uint groupId : SV_GroupID, uint groupIndex : SV_GroupIndex)
{
	if (id >= MaxThread || !Input[id])
		return;

	uint dest = PrefixSums[id] + GroupSums[groupId];
	Output[dest] = id;
	
	// Compute sort key from instanceType, lod and viewDistance
	float4 boundsData = InstanceBounds[id];
	float3 boundsCenter = boundsData.xyz - ViewPosition;
	float3 boundsExtents = R11G11B10ToFloat3(asuint(boundsData.w));
	
	// Calculate lod (Assume 0 for now)
	uint instanceType = InstanceTypeIds[id];
	InstanceTypeData instanceTypeData = InstanceTypeDatas[instanceType];
	
	uint maxLodCount = min(8, instanceTypeData.lodCount);
	uint lod = maxLodCount;
	float viewZ = dot(boundsCenter, ViewForward);
	float worldRadius = Max3(boundsExtents);
	float screenHeight = worldRadius * rcp(viewZ * TanHalfFov) * 0.5;
	
	[unroll]
	for (uint i = 0; i < maxLodCount; i++)
	{
		uint index = instanceTypeData.lodSizebufferPosition + i;
		float lodSize = LodSizes[index];
	
		if (screenHeight <= lodSize)
			continue;
			
		lod = i;
		break;
	}
	
	uint lodInfoIndex = instanceTypeData.lodRendererOffset + lod;
	InterlockedAdd(LodCounts[lodInfoIndex], 1);
	
	uint viewZNorm = saturate(LinearToDeviceDepth(viewZ)) * 65536.0;
	uint sortKey = lod | ((instanceType & 8192) << 3) | ((viewZNorm & 65536) << 16);
	SortKeysWrite[dest] = sortKey;
}

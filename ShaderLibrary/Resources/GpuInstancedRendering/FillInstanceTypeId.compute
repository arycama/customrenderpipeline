#pragma kernel FillData

#include "../../Common.hlsl"
#include "../../Packing.hlsl"
#include "../../MatrixUtils.hlsl"

struct Bounds
{
	float3 center, extents;
};

uint _Offset, _Count;

RWStructuredBuffer<uint> _InstanceTypeIds;
RWStructuredBuffer<float3x4> _PositionsResult;
RWStructuredBuffer<float> _LodFadesResult;
RWStructuredBuffer<float4> _InstanceBounds;

StructuredBuffer<float3x4> _PositionsInput;
StructuredBuffer<Bounds> _InstanceTypeBounds;
Buffer<uint> _InstanceTypeIdsInput;

[numthreads(1024, 1, 1)]
void FillData(uint id : SV_DispatchThreadID)
{
	if (id >= _Count)
		return;
	
	uint instanceType = _InstanceTypeIdsInput[id];
	float3x4 objectToWorld = _PositionsInput[id];
	Bounds bounds = _InstanceTypeBounds[instanceType];
	
	float3 inputMin = bounds.center - bounds.extents;
	float3 inputSize = bounds.extents * 2;
	float3 boundsMin, boundsMax;

	[unroll]
	for (float z = 0; z < 2; z++)
	{
		[unroll]
		for (float y = 0; y < 2; y++)
		{
			[unroll]
			for (float x = 0; x < 2; x++)
			{
				// Transform corners from object to world space, and calculate min/max for Frustum culling
				float3 objectPosition = inputMin + inputSize * float3(x, y, z);
				float3 worldPosition = MultiplyPoint3x4(objectToWorld, objectPosition);
				boundsMin = any(float3(x, y, z)) ? min(boundsMin, worldPosition) : worldPosition;
				boundsMax = any(float3(x, y, z)) ? max(boundsMax, worldPosition) : worldPosition;
			}
		}
	}
	
	float3 center = 0.5 * (boundsMax + boundsMin);
	float3 extents = 0.5 * (boundsMax - boundsMin);
	
	float4 packedBounds;
	packedBounds.xyz = center;
	packedBounds.w = asfloat(Float3ToR11G11B10(extents));
	
	_InstanceTypeIds[_Offset + id] = instanceType;
	_PositionsResult[_Offset + id] = objectToWorld;
	_LodFadesResult[_Offset + id] = 0;
	_InstanceBounds[_Offset + id] = packedBounds;

}

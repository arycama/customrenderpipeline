#include "../../Math.hlsl"
#include "../../PrefixSum.hlsl"

#pragma kernel RadixCount COUNT
#pragma kernel RadixScan SCAN
#pragma kernel RadixScatter SCATTER

#ifdef SCAN
	const static uint threadCount = 1024;
#else
	const static uint threadCount = 256;
#endif

RWStructuredBuffer<uint> KeysResult, DataResult, CountResult, ScanResult, TotalFalsesResult;
StructuredBuffer<uint> Keys, Data, GroupCounts, TotalGroupCount, TotalFalses, GroupScans;
uint BitIndex;

groupshared uint sharedSums[2 * threadCount];
groupshared uint SharedTotalFalses;

cbuffer DataLength
{
	uint TotalLength;
	uint pad0;
	uint pad1;
	uint pad2;
};

void PrefixSumSharedWrite(uint index, uint data)
{
	sharedSums[index] = data;
}

uint PrefixSumSharedRead(uint index)
{
	return sharedSums[index];
}

void PrefixSumOutputTotalCount(uint totalSum)
{
	#ifdef COUNT
		SharedTotalFalses = totalSum;
	#endif

	#ifdef SCAN
		TotalFalsesResult[0] = totalSum;
	#endif
}

[numthreads(threadCount, 1, 1)]
void RadixCount(uint id : SV_DispatchThreadID, uint groupIndex : SV_GroupIndex, uint groupId : SV_GroupID)
{
	// Count the elements for this block
	bool outOfRange = id >= TotalLength;
	uint key = outOfRange ? 0xFFFFFFFF : Keys[id];
	uint data = outOfRange ? 0 : Data[id];
	bool bit = (key >> BitIndex) & 0x1;
	
	sharedSums[groupIndex + ConflictFreeOffset(groupIndex)] = !bit;
	
	PrefixSum(groupIndex, threadCount);
	GroupMemoryBarrierWithGroupSync();
	
	// Shuffle in group memory based on counter
	uint f = sharedSums[groupIndex];
	uint t = groupIndex - f + SharedTotalFalses;
	uint d = bit ? t : f;
	
	// Write to global memory
	KeysResult[groupId * threadCount + d] = key;
	DataResult[groupId * threadCount + d] = data;
	
	// Write the final count out to global memory
	if (!groupIndex)
		CountResult[groupId] = SharedTotalFalses;
}

[numthreads(threadCount, 1, 1)]
void RadixScan(uint groupIndex : SV_GroupIndex)
{
	// Prefix sum the counts
	bool outOfRange = groupIndex >= TotalGroupCount[0];
	sharedSums[groupIndex] = outOfRange ? 0 : GroupCounts[groupIndex];
	
	PrefixSum(groupIndex, threadCount);
	GroupMemoryBarrierWithGroupSync();
	
	// Write the total prefix sum for each group
	ScanResult[groupIndex] = sharedSums[groupIndex];
}

[numthreads(threadCount, 1, 1)]
void RadixScatter(uint id : SV_DispatchThreadID, uint groupIndex : SV_GroupIndex, uint groupId : SV_GroupID)
{
	if (id >= TotalLength)
		return;

	uint key = Keys[id];
	uint data = Data[id];
	bool bit = (key >> BitIndex) & 0x1;
	
	uint groupScan = GroupScans[groupId];
	uint localOffset = bit ? GroupCounts[groupId] : 0;
	uint globalOffset = bit ? groupId * threadCount + TotalFalses[0] - groupScan : groupScan;
	uint globalDest = groupIndex - localOffset + globalOffset;
	
	// Write final results
	DataResult[globalDest] = data;
	KeysResult[globalDest] = key;
}

#include "../../Math.hlsl"
#include "../../PrefixSum.hlsl"

#pragma kernel RadixCount COUNT
#pragma kernel RadixScan SCAN
#pragma kernel RadixScatter SCATTER

#ifdef SCAN
const static uint threadCount = 256;
#else
const static uint threadCount = 128;
#endif

RWStructuredBuffer<uint> KeysResult, DataResult, CountResult;
RWStructuredBuffer<uint4> ScanResult;
StructuredBuffer<uint> Keys, Data, TotalGroupCount, GroupCounts;
StructuredBuffer<uint4> GroupScans;
uint BitIndex;

groupshared uint sharedSums[threadCount];

cbuffer DataLength
{
	uint TotalLength;
	uint pad0;
	uint pad1;
	uint pad2;
};

void PrefixSumSharedWrite(uint index, uint data)
{
	sharedSums[index] = data;
}

uint PrefixSumSharedRead(uint index)
{
	return sharedSums[index];
}

[numthreads(threadCount, 1, 1)]
void RadixCount(uint id : SV_DispatchThreadID, uint groupIndex : SV_GroupIndex, uint groupId : SV_GroupID)
{
	// Count the elements for this block
	bool outOfRange = id >= TotalLength;
	uint key = outOfRange ? UintMax : Keys[id];
	uint data = outOfRange ? 0 : Data[id];
	uint value = (key >> BitIndex) & 0x3;
	
	// Count and sort
	uint4 packedValues = (value == uint4(0, 1, 2, 3)) << uint4(0, 8, 16, 24);
	uint packedValue = packedValues.x | packedValues.y | packedValues.z | packedValues.w;
	
	uint packedSum;
	uint packedOffset = PrefixSum(packedValue, groupIndex, threadCount, packedSum);
	
	uint4 offsets = (packedOffset >> uint4(0, 8, 16, 24)) & 0xFF;
	uint4 sums = (packedSum >> uint4(0, 8, 16, 24)) & 0xFF;
	
	if (!groupIndex)
		CountResult[groupId] = packedSum;
	
	offsets.yzw += sums.x;
	offsets.zw += sums.y;
	offsets.w += sums.z;
		
	uint d = offsets[value];
	
	// Write to global memory
	KeysResult[groupId * threadCount + d] = key;
	DataResult[groupId * threadCount + d] = data;
}

[numthreads(threadCount, 1, 1)]
void RadixScan(uint groupIndex : SV_GroupIndex)
{
	// Prefix sum the counts
	bool outOfRange = groupIndex >= TotalGroupCount[0];
	uint packedCount = outOfRange ? 0 : GroupCounts[groupIndex];
	uint4 counts = (packedCount >> uint4(0, 8, 16, 24)) & 0xFF;
	
	uint4 sums, offsets;
	offsets.x = PrefixSum(counts.x, groupIndex, threadCount, sums.x);
	offsets.y = PrefixSum(counts.y, groupIndex, threadCount, sums.y);
	offsets.z = PrefixSum(counts.z, groupIndex, threadCount, sums.z);
	offsets.w = PrefixSum(counts.w, groupIndex, threadCount, sums.w);
	
	offsets.yzw += sums.x;
	offsets.zw += sums.y;
	offsets.w += sums.z;
	
	ScanResult[groupIndex] = offsets;
}

[numthreads(threadCount, 1, 1)]
void RadixScatter(uint id : SV_DispatchThreadID, uint groupIndex : SV_GroupIndex, uint groupId : SV_GroupID)
{
	if (id >= TotalLength)
		return;

	uint key = Keys[id];
	uint value = (key >> BitIndex) & 0x3;
	
	uint packedLocalOffsets = GroupCounts[groupId];
	uint4 localOffsets = (packedLocalOffsets >> uint4(0, 8, 16, 24)) & 0xFF;
	
	localOffsets.w += localOffsets.z;
	localOffsets.zw += localOffsets.y;
	localOffsets.yzw += localOffsets.x;
	
	uint localOffset = value ? localOffsets[value - 1] : 0;
	uint globalOffset = GroupScans[groupId][value];
	
	uint globalDest = groupIndex - localOffset + globalOffset;
	
	// Write final results
	DataResult[globalDest] = Data[id];
	KeysResult[globalDest] = key;
}

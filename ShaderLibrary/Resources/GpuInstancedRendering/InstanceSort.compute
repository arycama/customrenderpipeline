#include "../../Math.hlsl"
#include "../../PrefixSum.hlsl"

#pragma kernel RadixCount COUNT
#pragma kernel RadixScan SCAN
#pragma kernel RadixScatter SCATTER
#pragma enable_d3d11_debug_symbols

#ifdef SCAN
	const static uint threadCount = 1024;
#else
	const static uint threadCount = 256;
#endif

const static uint log2ThreadCount = firstbitlow(threadCount);

StructuredBuffer<uint> TotalInstanceCount, SortKeys;
uint BitIndex;

groupshared uint sharedSums[threadCount];

void PrefixSumSharedWrite(uint index, uint data)
{
	sharedSums[index] = data;
}

uint PrefixSumSharedRead(uint index)
{
	return sharedSums[index];
}

RWStructuredBuffer<uint> CountResult;

[numthreads(threadCount, 1, 1)]
void RadixCount(uint id : SV_DispatchThreadID, uint groupIndex : SV_GroupIndex, uint groupId : SV_GroupID)
{
	// Count the elements for this block
	bool outOfRange = id >= TotalInstanceCount[0];
	uint key = outOfRange ? 0xFFFFFFFF : SortKeys[id];
	bool bit = (key >> BitIndex) & 0x1;
		
	sharedSums[groupIndex] = !bit;
	
	GroupMemoryBarrierWithGroupSync();
	
	uint totalFalses;
	PrefixSum(groupIndex, threadCount, log2ThreadCount, totalFalses);
	
	// Could write out the prefix sum results here.. but instead we just perform it again in the sort kernel
	// Write the final count out to global memory
	if (!groupIndex)
		CountResult[groupId] = totalFalses;
}

StructuredBuffer<uint> Count, TotalGroupCount;
RWStructuredBuffer<uint> ScanResult, TotalFalsesResult;

[numthreads(threadCount, 1, 1)]
void RadixScan(uint groupIndex : SV_GroupIndex)
{
	bool outOfRange = groupIndex >= TotalGroupCount[0];

	// Prefix sum the counts
	sharedSums[groupIndex] = outOfRange ? 0 : Count[groupIndex];
	
	GroupMemoryBarrierWithGroupSync();
	
	uint totalFalses;
	PrefixSum(groupIndex, threadCount, log2ThreadCount, totalFalses);
	
	GroupMemoryBarrierWithGroupSync();
	
	// Write the total prefix sum for each group
	ScanResult[groupIndex] = sharedSums[groupIndex];
	
	// Write the toal global number of falses
	if (!groupIndex)
		TotalFalsesResult[0] = totalFalses;
}

RWStructuredBuffer<uint> ScatterDataResult, ScatterKeysResult;
StructuredBuffer<uint> ScatterData, TotalFalses, GroupScans;

groupshared uint sharedKeys[threadCount];

[numthreads(threadCount, 1, 1)]
void RadixScatter(uint id : SV_DispatchThreadID, uint groupIndex : SV_GroupIndex, uint groupId : SV_GroupID)
{
	// Count the elements for this block
	bool outOfRange = id >= TotalInstanceCount[0];
	uint key = outOfRange ? 0xFFFFFFFF : SortKeys[id];
	bool bit = (key >> BitIndex) & 0x1;
	
	sharedSums[groupIndex] = !bit;
	
	GroupMemoryBarrierWithGroupSync();
	
	uint totalFalses;
	PrefixSum(groupIndex, threadCount, log2ThreadCount, totalFalses);
	
	GroupMemoryBarrierWithGroupSync();
	
	uint groupScan = GroupScans[groupId]; // TODO: Shared memory
	
	uint i = id;
	uint f = sharedSums[groupIndex] + groupScan; // Add the group scan to the result.
	uint t = i - f + TotalFalses[0]; // TODO: Is it better to load this into shared memory?
	uint d = bit ? t : f;
	
	// Write final results
	ScatterDataResult[d] = ScatterData[id];
	ScatterKeysResult[d] = SortKeys[id];
}

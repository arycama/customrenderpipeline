#include "../../Common.hlsl"
#include "../../GBuffer.hlsl"
#include "../../Random.hlsl"
#include "../../Samplers.hlsl"
#include "../../Lighting.hlsl"
#include "../../ImageBasedLighting.hlsl"
#include "../../Raytracing.hlsl"
#include "../../Water/WaterPrepassCommon.hlsl"

Texture2D<float4> _NormalRoughness;
Texture2D<float> _Depth;

//RWTexture2D<float4> HitResult;
//RWTexture2D<float4> HitColor;
RWTexture2D<float3> RefractionResult, ScatterResult;
Texture2D<uint2> _Stencil;

float3 _Extinction;
float _RaytracingBias, _RaytracingDistantBias;
float3 _Color;

[shader("miss")]
void MissShader(inout RayPayload payload : SV_RayPayload)
{
	payload.packedColor = 0.0;
	payload.hitDistance = 0.0;
}

[shader("raygeneration")]
void RayGeneration()
{
	uint2 position = DispatchRaysIndex().xy;
	float depth = _Depth[position];
	uint stencil = _Stencil[position].g;
	bool isWater = stencil & 4;
	
	if (depth == 0.0 || !isWater)
		return;
	
	float4 normalRoughness = _NormalRoughness[position];
	float2 u = Noise2D(position);
	
	float3 worldPosition = PixelToWorld(float3(position + 0.5, depth));
	float3 V = normalize(-worldPosition);
	
	float NdotV;
	float3 N = GBufferNormal(normalRoughness, V, NdotV);
	
	// We also need to undo the planet curve, since it's not accounted for in the BLAS
	worldPosition = PlanetCurveInverse(worldPosition);
	
	float roughness = Sq(normalRoughness.a);
	
	bool isFrontFace;
	float3 triangleNormal = GetTriangleNormal(position.xy, V, isFrontFace);
	
	//float rcpPdf;
	float eta = isFrontFace ? rcp(1.34) : 1.34;
	float3 L = refract(-V, N, eta); //	ImportanceSampleGGX(roughness, N, V, u, NdotV, rcpPdf);
	bool isTotalInternalReflection = all(L == 0.0);
	
	float _RaytracingBias = 0.1, _RaytracingDistantBias = 0.1;
	
	RayDesc ray;
	ray.Origin = worldPosition + _ViewPosition + N * EvaluateRayTracingBias(worldPosition, _Near, _Far, _RaytracingBias, _RaytracingDistantBias);
	ray.Direction = L;
	ray.TMin = 0.0;
	ray.TMax = 1e10f;
	
	RayPayload payload;
	payload.packedColor = 0.0;
	payload.hitDistance = 0.0;
	
	// In order to achieve filtering for the textures, we need to compute the spread angle of the pixel
	float distanceToCamera = length(worldPosition);
	payload.cone.width = _RaytracingPixelSpreadAngle * distanceToCamera;
	payload.cone.spreadAngle = _RaytracingPixelSpreadAngle + RoughnessToSpreadAngle(roughness);
	
	if (!isTotalInternalReflection)
	{
		uint flags = RAY_FLAG_CULL_BACK_FACING_TRIANGLES;
		TraceRay(SceneRaytracingAccelerationStructure, flags, 0xFF, 0, 1, 0, ray, payload);
	}
	
	bool validHit = payload.hitDistance;
	float outDepth;
	float3 color, hitRay;
	if (validHit && !isTotalInternalReflection)
	{
		hitRay = L * payload.hitDistance;
		color = R11G11B10ToFloat3(payload.packedColor);
		outDepth = Linear01Depth(depth);
		color *= exp(-_Extinction * (isFrontFace ? payload.hitDistance : length(worldPosition)));
	}
	else
	{
		color = isFrontFace || isTotalInternalReflection ? 0.0 : _SkyReflection.SampleLevel(_LinearClampSampler, L, 0.0);
		hitRay = L;
		outDepth = 0.0;
	}
	
	RefractionResult[position] = color;
	
	// Select random channel
	float2 noise = Noise2D(position.xy);
	uint channelIndex = noise.y < 1.0 / 3.0 ? 0 : (noise.y < 2.0 / 3.0 ? 1 : 2);
	float xi = min(noise.x, 0.999); // xi of 1 maps to infinity, so clamp
	float3 c = _Extinction;
	float3 cp = Select(_Extinction, channelIndex);
	float l = _DirectionalLights[0].direction.y;
	float v = V.y;
	float b = validHit ? payload.hitDistance : 1e+32;
		
	float t = -(l * log((xi * (exp(b * cp * (-v / l - 1)) - 1) + 1))) / (cp * (l + v));
	float3 pdf = -c * (l + v) * exp(c * t * (-v / l - 1)) / (l * (exp(b * c * (-v / l - 1)) - 1));
	float weight = rcp(dot(pdf, rcp(3.0)));
	
	float3 P = ray.Origin + ray.Direction * t - _ViewPosition;
	
	float3 luminance = 0.0;
	float planetDistance = DistanceToNearestAtmosphereBoundary(_ViewHeight, -V.y);
	
	// Todo: share/merge with non raytraced code?
	//#if defined(LIGHT_COUNT_ONE) || defined(LIGHT_COUNT_TWO)
	float attenuation = GetShadow(P, 0, false);
	attenuation *= CloudTransmittance(P);
	float shadowDistance0 = max(0.0, worldPosition.y - P.y) / max(1e-6, saturate(_LightDirection0.y));
	float3 shadowPosition = MultiplyPoint3x4(_WaterShadowMatrix1, P);
	if (all(saturate(shadowPosition.xyz) == shadowPosition.xyz))
	{
		float shadowDepth = _WaterShadows.SampleLevel(_LinearClampSampler, shadowPosition.xy, 0.0);
		shadowDistance0 = saturate(shadowDepth - shadowPosition.z) * _WaterShadowFar;
	}
	
	float3 asymmetry = exp(-_Extinction * (shadowDistance0 + t));
	float LdotV0 = dot(_LightDirection0, L);
	float phase = lerp(MiePhase(LdotV0, -0.3), MiePhase(LdotV0, 0.85), asymmetry);
	float3 lightColor0 = phase * _LightColor0 * TransmittanceToAtmosphere(_ViewHeight, -V.y, _LightDirection0.y, length(worldPosition));
	lightColor0 *= GetCaustics(P + _ViewPosition, _LightDirection0, true);
	luminance += lightColor0 * attenuation * asymmetry;
	
	luminance *= _Extinction * weight * _Exposure;
	
	// Ambient 
	float3 finalTransmittance = exp(-t * _Extinction);
	luminance += AmbientLight(float3(0.0, 1.0, 0.0)) * (1.0 - finalTransmittance);
	luminance *= _Color;
	
	ScatterResult[position] = luminance;
}

#include "../../Common.hlsl"
#include "../../Color.hlsl"
#include "../../GBuffer.hlsl"
#include "../../Random.hlsl"
#include "../../Samplers.hlsl"
#include "../../Lighting.hlsl"
#include "../../ImageBasedLighting.hlsl"
#include "../../Raytracing.hlsl"
#include "../../Water/WaterPrepassCommon.hlsl"

RWTexture2D<float4> HitResult;
RWTexture2D<float4> HitColor;

float _RaytracingBias, _RaytracingDistantBias;

[shader("miss")]
void MissShader(inout RayPayload payload : SV_RayPayload)
{
	payload.packedColor = 0.0;
	payload.hitDistance = 0.0;
}

[shader("raygeneration")]
void RayGeneration()
{
	uint2 position = DispatchRaysIndex().xy;
	float depth = CameraDepth[position];
	if(depth == 0.0)
		return;
	
	float4 normalRoughness = GBufferNormalRoughness[position];
	float2 u = Noise2D(position);
	
	float3 worldPosition = PixelToWorldPosition(float3(position + 0.5, depth));
	float3 V = normalize(-worldPosition);
	
	float NdotV;
	float3 N = GBufferNormal(normalRoughness, V, NdotV);
	
	float roughness = Sq(normalRoughness.a);
	float rcpPdf;
	float3 L = ImportanceSampleGGX(roughness, N, V, u, NdotV, rcpPdf);
	
	RayDesc ray;
	ray.Origin = worldPosition + ViewPosition + N * EvaluateRayTracingBias(worldPosition, Near, Far, _RaytracingBias, _RaytracingDistantBias);
	ray.Direction = L;
	ray.TMin = 0.0;
	ray.TMax = 1e10f;
	
	RayPayload payload;
	payload.packedColor = 0.0;
	payload.hitDistance = 0.0;
	
	// In order to achieve filtering for the textures, we need to compute the spread angle of the pixel
	float distanceToCamera = length(worldPosition);
	payload.cone.width = _RaytracingPixelSpreadAngle * distanceToCamera;
	payload.cone.spreadAngle = _RaytracingPixelSpreadAngle + RoughnessToSpreadAngle(roughness);
	
	uint flags = RAY_FLAG_CULL_BACK_FACING_TRIANGLES;
	TraceRay(SceneRaytracingAccelerationStructure, flags, 0xFF, 0, 1, 0, ray, payload);
	
	bool validHit = payload.hitDistance;
	float outDepth;
	float3 color, hitRay;
	if(validHit)
	{
		hitRay = L * payload.hitDistance;
		color = R11G11B10ToFloat3(payload.packedColor);
		outDepth = Linear01Depth(depth);
	}
	else
	{
		color = 0.0;
		hitRay = L;
		outDepth = 0.0;
	}
	
	bool isFrontFace;
	float3 triangleNormal = GetTriangleNormal(position.xy, V, isFrontFace);
	if (!isFrontFace & validHit)
	{
		color *= exp(-payload.hitDistance * _WaterExtinction);
	}
	
	HitColor[position] = float4(color, rcpPdf);
	HitResult[position] = float4(hitRay, outDepth);
}

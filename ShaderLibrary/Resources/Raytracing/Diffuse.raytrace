#include "../../Common.hlsl"
#include "../../GBuffer.hlsl"
#include "../../Random.hlsl"
#include "../../Samplers.hlsl"
#include "../../Lighting.hlsl"
#include "../../Raytracing.hlsl"

Texture2D<float4> _NormalRoughness;
Texture2D<float> _Depth;

RWTexture2D<float3> HitColor;
RWTexture2D<float4> HitResult;

float _RaytracingBias, _RaytracingDistantBias;

#pragma max_recursion_depth 5

[shader("miss")]
void MissShader(inout RayPayload payload : SV_RayPayload)
{
	payload.packedColor = 0.0;
	payload.hitDistance = 0.0;
}

[shader("miss")]
void MissShaderShadow(inout RayPayloadShadow payload : SV_RayPayload)
{
	payload.hasHit = false;
}

[shader("raygeneration")]
void RayGeneration()
{
	uint2 position = DispatchRaysIndex().xy;
	float depth = _Depth[position];
	if(depth == 0.0)
		return;
	
	float3 worldPosition = PixelToWorld(float3(position + 0.5, depth));
	float3 V = normalize(-worldPosition);
	
	float NdotV;
	float3 N = GBufferNormal(position, _NormalRoughness, V, NdotV);
	
	float3 noise3DCosine = Noise3DCosine(position);
	float3 L = FromToRotationZ(N, noise3DCosine);
	
	// We also need to undo the planet curve, since it's not accounted for in the BLAS
	worldPosition = PlanetCurveInverse(worldPosition);
	
	RayDesc ray;
	ray.Origin = worldPosition + N * EvaluateRayTracingBias(worldPosition, _Near, _Far, _RaytracingBias * 10, _RaytracingDistantBias * 10);
	ray.Direction = L;
	ray.TMin = 0.0;
	ray.TMax = 1e10f;
	
	RayPayload payload;
	payload.packedColor = 0.0;
	payload.hitDistance = 0.0;
	
	uint flags = RAY_FLAG_CULL_BACK_FACING_TRIANGLES;
	TraceRay(SceneRaytracingAccelerationStructure, flags, 0xFF, 1, 2, 0, ray, payload);
	
	bool validHit = payload.hitDistance;
	float outDepth;
	float3 color, hitRay;
	if(validHit)
	{
		hitRay = L * payload.hitDistance;
		color = R11G11B10ToFloat3(payload.packedColor);
		outDepth = Linear01Depth(depth);
	}
	else
	{
		color = 0.0;
		hitRay = L;
		outDepth = 0.0;
	}
	
	HitColor[position] = color;
	HitResult[position] = float4(hitRay, outDepth);
}

#include "../../Color.hlsl"
#include "../../Common.hlsl"
#include "../../GBuffer.hlsl"
#include "../../Random.hlsl"
#include "../../Samplers.hlsl"
#include "../../Lighting.hlsl"
#include "../../Raytracing.hlsl"

RWTexture2D<float4> HitColor, HitResult;

float _RaytracingBias, _RaytracingDistantBias;

[shader("miss")]
void MissShader(inout RayPayload payload : SV_RayPayload)
{
	payload.packedColor = 0.0;
	payload.hitDistance = 0.0;
}

[shader("raygeneration")]
void RayGeneration()
{
	uint2 position = DispatchRaysIndex().xy;
	float depth = Depth[position];
	if(depth == 0.0)
		return;
	
	float3 worldPosition = PixelToWorldPosition(float3(position + 0.5, depth));
	float3 V = normalize(-worldPosition);
	
	float NdotV;
	float3 N = GBufferNormal(position, NormalRoughness, V, NdotV);
	
	float3 noise3DCosine = Noise3DCosine(position);
	float rcpPdf = Pi * rcp(noise3DCosine.z);
	float3 L = FromToRotationZ(N, noise3DCosine);
	
	// We also need to undo the planet curve, since it's not accounted for in the BLAS
	worldPosition = PlanetCurveInverse(worldPosition);
	
	RayDesc ray;
	ray.Origin = worldPosition + ViewPosition + N * EvaluateRayTracingBias(worldPosition, Near, Far, _RaytracingBias, _RaytracingDistantBias);
	ray.Direction = L;
	ray.TMin = 0.0;
	ray.TMax = 1e10f;
	
	RayPayload payload;
	payload.packedColor = 0.0;
	payload.hitDistance = 0.0;
	
	// In order to achieve filtering for the textures, we need to compute the spread angle of the pixel
	float distanceToCamera = length(worldPosition);
	payload.cone.width = _RaytracingPixelSpreadAngle * distanceToCamera;
	payload.cone.spreadAngle = _RaytracingPixelSpreadAngle + RoughnessToSpreadAngle(1);
	
	uint flags = RAY_FLAG_CULL_BACK_FACING_TRIANGLES;
	TraceRay(SceneRaytracingAccelerationStructure, flags, 0xFF, 0, 1, 0, ray, payload);
	
	bool validHit = payload.hitDistance;
	float outDepth;
	float3 color, hitRay;
	if(validHit)
	{
		hitRay = L * payload.hitDistance;
		color = R11G11B10ToFloat3(payload.packedColor);
		outDepth = Linear01Depth(depth);
	}
	else
	{
		color = 0.0;
		hitRay = L;
		outDepth = 0.0;
	}
	
	HitColor[position] = float4(color, rcpPdf);
	HitResult[position] = float4(hitRay, outDepth);
}
